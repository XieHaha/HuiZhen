apply plugin: 'com.android.application'

def releaseTime() {
    return new Date().format("MM月dd日", TimeZone.getTimeZone("UTC"))
}

android {
    compileSdkVersion 27
    defaultConfig {
        minSdkVersion 19
        targetSdkVersion 27
        versionCode 21
        versionName "3.0.1"
        flavorDimensions "default"
        multiDexEnabled true
        testInstrumentationRunner "android.support.test.runner.AndroidJUnitRunner"
        ndk {
            abiFilters "armeabi-v7a", "armeabi"
        }
    }

    flavorDimensions "zyc"
    productFlavors {
        zyc {}
        develop {}
        beta {}
        Tencent {}
        Huawei {}
        Xiaomi {}
        Oppo {}
        Vivo {}
    }

    productFlavors.all { flavor ->
        dimension "zyc"
        if ("beta" == name) {
            applicationId "com.yht.yihuantong"
            buildConfigField "String", "BASE_BASIC_URL", "\"http://192.168.0.100:7071/\""
            resValue "string", "APP_NAME", "\"会珍医生测试版\""
            manifestPlaceholders = [ease_appkey        : "1105190122042534#dp-test-app",
                                    jpush_appkey       : "ad21b958f6040b2b0ae89f46",
                                    UMENG_CHANNEL_VALUE: "beta"]
        } else if ("develop" == name) {
            applicationId "com.yht.yihuantong"
            buildConfigField "String", "BASE_BASIC_URL", "\"http://192.168.0.126:7071/\""
            resValue "string", "APP_NAME", "\"会珍医生开发版\""
            manifestPlaceholders = [ease_appkey        : "1110180330253249#dpappproject",
                                    jpush_appkey       : "8b803f7221a77f19b78b3523",
                                    UMENG_CHANNEL_VALUE: "develop"]

        } else if ("zyc" == name) {
            applicationId "com.yht.yihuantong"
            buildConfigField "String", "BASE_BASIC_URL", "\"http://doctor-pre.med-value.com/\""
            resValue "string", "APP_NAME", "\"会珍医生灰度版\""
            flavor.manifestPlaceholders = [ease_appkey        : "1110180330253249#dpappproject",
                                           jpush_appkey       : "8b803f7221a77f19b78b3523",
                                           UMENG_CHANNEL_VALUE: "zyc"]
        } else {
            applicationId "com.yht.yihuantong"
            buildConfigField "String", "BASE_BASIC_URL", "\"http://doctor.med-value.com/\""
            resValue "string", "APP_NAME", "\"会珍医生版\""
            flavor.manifestPlaceholders = [ease_appkey        : "1110180330253249#dpappproject",
                                           jpush_appkey       : "8b803f7221a77f19b78b3523",
                                           UMENG_CHANNEL_VALUE: name]
        }

    }

    signingConfigs {
        release {
            storeFile file('F:/HuiZhen/app/zyc.jks')
            storePassword 'android'
            keyAlias = '智予成'
            keyPassword '123456'
        }
        debug {
            keyAlias '测试'
            keyPassword '999999'
            storeFile file('F:/HuiZhen/app/zyc.jks')
            storePassword 'android'
        }
    }


    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android-optimize.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release
        }

        debug {
            signingConfig signingConfigs.debug
        }
    }

    android.applicationVariants.all { variant ->
        variant.outputs.all {
            if (variant.name.endsWith("Debug")) {
                //debug包
                outputFileName = "DEBUG_${variant.productFlavors[0].name}V${variant.versionName}-${releaseTime()}.apk"
            } else {
                //release包
                outputFileName = "RELEASE_${variant.productFlavors[0].name}V${variant.versionName}-${releaseTime()}.apk"
            }
        }
    }
    compileOptions {
        targetCompatibility JavaVersion.VERSION_1_8
        sourceCompatibility JavaVersion.VERSION_1_8
    }
    lintOptions {
        disable 'GoogleAppIndexingWarning'
    }
}

dependencies {
    implementation fileTree(include: ['*.jar'], dir: 'libs')
    implementation project(':frame')
    implementation project(':easeui')
    //腾讯X5
    implementation files('libs/tbs_sdk.jar')
    implementation 'com.android.support.constraint:constraint-layout:1.1.3'
    testImplementation 'junit:junit:4.12'
    androidTestImplementation 'com.android.support.test:runner:1.0.2'
    androidTestImplementation 'com.android.support.test.espresso:espresso-core:3.0.2'

    //微信登录  不包含统计
    implementation 'com.tencent.mm.opensdk:wechat-sdk-android-without-mta:+'
    //注解
    annotationProcessor 'com.jakewharton:butterknife-compiler:8.8.0'
    //gif动图
    implementation 'pl.droidsonroids.gif:android-gif-drawable:1.2.17'
}
